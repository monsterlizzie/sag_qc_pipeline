
// Run Shovill to get de novo assembly
process ASSEMBLY_SHOVILL {
    label 'shovill_container'
    label 'farm_assembler'

    tag "$sample_id"

    input:
    tuple val(sample_id), path(read1), path(read2), path(unpaired)
    val min_contig_length
    val assembler_thread

    output:
    tuple val(sample_id), path(fasta), emit: assembly

    script:
    fasta="${sample_id}.contigs.fasta"
    thread="$assembler_thread"
    """
    READ1="$read1"
    READ2="$read2"
    MIN_CONTIG_LENGTH="$min_contig_length"
    FASTA="$fasta"
    THREAD="$thread"

    source get_assembly_shovill.sh
    """
}


// Run QUAST to assess assembly quality
process ASSEMBLY_ASSESS {
    label 'quast_container'
    label 'farm_low'

    tag "$sample_id"

    input:
    tuple val(sample_id), path(assembly)

    output:
    tuple val(sample_id), path('results/report.tsv'), emit: report

    script:
    """
    quast.py -o results "$assembly"
    """
}


// Determine assembly QC result from QUAST report and read base count
process ASSEMBLY_QC {
    label 'bash_container'
    label 'farm_low'

    tag "$sample_id"

    input:
    tuple val(sample_id), path(report), val(bases)
    val(qc_contigs)
    val(qc_length_low)
    val(qc_length_high)
    val(qc_depth)

    output:
    tuple val(sample_id), env('ASSEMBLY_QC'), emit: result
    tuple val(sample_id), path(assembly_qc_report), emit: report

    script:
    assembly_qc_report='assembly_qc_report.csv'
    """
    REPORT="$report"
    BASES="$bases"
    QC_CONTIGS="$qc_contigs"
    QC_LENGTH_LOW="$qc_length_low"
    QC_LENGTH_HIGH="$qc_length_high"
    QC_DEPTH="$qc_depth"
    ASSEMBLY_QC_REPORT="$assembly_qc_report"

    source get_assembly_qc.sh
    """
}